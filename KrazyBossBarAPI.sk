#
#  /$$$$$$$                               /$$$$$$$                       /$$$$$$  /$$$$$$$  /$$$$$$
# | $$__  $$                             | $$__  $$                     /$$__  $$| $$__  $$|_  $$_/
# | $$  \ $$  /$$$$$$   /$$$$$$$ /$$$$$$$| $$  \ $$  /$$$$$$   /$$$$$$ | $$  \ $$| $$  \ $$  | $$  
# | $$$$$$$  /$$__  $$ /$$_____//$$_____/| $$$$$$$  |____  $$ /$$__  $$| $$$$$$$$| $$$$$$$/  | $$  
# | $$__  $$| $$  \ $$|  $$$$$$|  $$$$$$ | $$__  $$  /$$$$$$$| $$  \__/| $$__  $$| $$____/   | $$  
# | $$  \ $$| $$  | $$ \____  $$\____  $$| $$  \ $$ /$$__  $$| $$      | $$  | $$| $$        | $$  
# | $$$$$$$/|  $$$$$$/ /$$$$$$$//$$$$$$$/| $$$$$$$/|  $$$$$$$| $$      | $$  | $$| $$       /$$$$$$
# |_______/  \______/ |_______/|_______/ |_______/  \_______/|__/      |__/  |__/|__/      |______/
#
#
#	AUTHOR: 									KrazyManJ
#	VERSION:									1.0
#	MC-VERSIONS:								1.17
#	POSSIBLY MC-VERSIONS (BY DOCUMENTATION):	1.11,1.12,1.13,1.14,1.15,1.16,1.17
#
#	This skript API (addon) can manipulate with bossbars. Create it, modify look of it, or remove it!
#	
#	NOTE: Those bossbars are server-side, they are saved in bukkit. If you need client-side bossbar,
#		  search for packets bossbar manipulation!
#  ______                                               __               
# |      \                                             |  \              
#  \$$$$$$ ______ ____    ______    ______    ______  _| $$_     _______ 
#   | $$  |      \    \  /      \  /      \  /      \|   $$ \   /       \
#   | $$  | $$$$$$\$$$$\|  $$$$$$\|  $$$$$$\|  $$$$$$\\$$$$$$  |  $$$$$$$
#   | $$  | $$ | $$ | $$| $$  | $$| $$  | $$| $$   \$$ | $$ __  \$$    \ 
#  _| $$_ | $$ | $$ | $$| $$__/ $$| $$__/ $$| $$       | $$|  \ _\$$$$$$\
# |   $$ \| $$ | $$ | $$| $$    $$ \$$    $$| $$        \$$  $$|       $$
#  \$$$$$$ \$$  \$$  \$$| $$$$$$$   \$$$$$$  \$$         \$$$$  \$$$$$$$ 
#                       | $$                                             
#                       | $$                                             
#                        \$$                                             
#
#
#	⚠             IMPORTANT TO MAKE THIS WHOLE SKRIPT WORK             ⚠
#
import:
	org.bukkit.Bukkit

	org.bukkit.boss.BossBar
	org.bukkit.boss.BarColor
	org.bukkit.boss.BarStyle
	org.bukkit.boss.BarFlag

	org.bukkit.NamespacedKey

	ch.njol.skript.Skript
#
#  __    __    __      __  __  __    __      __                     
# |  \  |  \  |  \    |  \|  \|  \  |  \    |  \                    
# | $$  | $$ _| $$_    \$$| $$ \$$ _| $$_    \$$  ______    _______ 
# | $$  | $$|   $$ \  |  \| $$|  \|   $$ \  |  \ /      \  /       \
# | $$  | $$ \$$$$$$  | $$| $$| $$ \$$$$$$  | $$|  $$$$$$\|  $$$$$$$
# | $$  | $$  | $$ __ | $$| $$| $$  | $$ __ | $$| $$    $$ \$$    \ 
# | $$__/ $$  | $$|  \| $$| $$| $$  | $$|  \| $$| $$$$$$$$ _\$$$$$$\
#  \$$    $$   \$$  $$| $$| $$| $$   \$$  $$| $$ \$$     \|       $$
#   \$$$$$$     \$$$$  \$$ \$$ \$$    \$$$$  \$$  \$$$$$$$ \$$$$$$$ 
#                                                      
#	Used to get yourself some fixed bossbar values, specifically
#	flags, colors, and styles of boss bar.
#	Usefull when you want to create random bossbar!
#
#	⚠ DO NOT REMOVE THOSE, BECAUSE THIS API USES THEM TO! ⚠
#
expression all boss bar flags:
	return type: strings
	get:
		return "CREATE_FOG","DARKEN_SKY","PLAY_BOSS_MUSIC"

expression all boss bar styles:
	return type: strings
	get:
		return "SOLID","SEGMENTED_6","SEGMENTED_10","SEGMENTED_12","SEGMENTED_20"

expression all boss bar colors:
	return type: strings
	get:
		return "BLUE","GREEN","PINK","PURPLE","RED","WHITE","YELLOW"
#
#  ________                                                              __                               
# |        \                                                            |  \                              
# | $$$$$$$$ __    __   ______    ______    ______    _______   _______  \$$  ______   _______    _______ 
# | $$__    |  \  /  \ /      \  /      \  /      \  /       \ /       \|  \ /      \ |       \  /       \
# | $$  \    \$$\/  $$|  $$$$$$\|  $$$$$$\|  $$$$$$\|  $$$$$$$|  $$$$$$$| $$|  $$$$$$\| $$$$$$$\|  $$$$$$$
# | $$$$$     >$$  $$ | $$  | $$| $$   \$$| $$    $$ \$$    \  \$$    \ | $$| $$  | $$| $$  | $$ \$$    \ 
# | $$_____  /  $$$$\ | $$__/ $$| $$      | $$$$$$$$ _\$$$$$$\ _\$$$$$$\| $$| $$__/ $$| $$  | $$ _\$$$$$$\
# | $$     \|  $$ \$$\| $$    $$| $$       \$$     \|       $$|       $$| $$ \$$    $$| $$  | $$|       $$
#  \$$$$$$$$ \$$   \$$| $$$$$$$  \$$        \$$$$$$$ \$$$$$$$  \$$$$$$$  \$$  \$$$$$$  \$$   \$$ \$$$$$$$ 
#                     | $$                                                                                
#                     | $$                                                                                
#                      \$$                                                                                
#
#	All of expressions for creating and editing boss bars. If some of them would'nt work, check console,
#	because all of errors from this API is showing there.
#
#	CREATING NEW BOSSBAR
#
#	This expression will create new bossbar and save it to variable to be accessed with all expressions down bellow.
#	You have to set all parameters of boss bar except flags, because they are irrelevant
#
#	PARAMETERS:
#
#	- key: 		is something like ID (in Java exactly named NamespacedKey), you can call bossbar from another event/function by that.
#
#				⚠ IT IS RECOMMENDED TO USE KEY WITHOUT SPACE! But this API can handle that so it's not that big deal :)
#
#	- title: 			exactly what it says, specifically shown text above bossbar (you can use colors, and also hex colors by formatting <##afAF09>)
#	- progress: 		value of bossbar, also called damage, must be value between 0 and 1
#	- color:			color of bossbar, you can only choose from "BLUE","GREEN","PINK","PURPLE","RED","WHITE","YELLOW"
#	- style:			style of bossbar, you can choose from "SOLID","SEGMENTED_6","SEGMENTED_10","SEGMENTED_12","SEGMENTED_20"
#	- flags:			OPTIONAL => you can set if bossbar will create fog, play boss music or make sky little bit darker
#	- players:			players, for that this bossbar will be shown
#	- hiding old one:	you can add this to the end of creating to hide old one with same key. If you don't use it, old bossbar will just stay on screen,
#						until player disconnets.
#
#	ALL OF THOSE PARAMETERS CAN BE MODIFIED LATER!
#
#	EXAMPLE (splitted into two lines because of length):
# 
#	set {_bossbar} to new boss bar with key "welcome-bossbar", title "&aWelcome to our server!",
#	progress 0.25, color "GREEN", style "SOLID", flags ("CREATE_FOG","DARKEN_SKY") for all players with hiding old one
#
expression new boss[ ]bar with key %string%[,] title %string%[,] progress %number%[,] color %string%[,] style %string%[[,] flags %-strings%] for %players%(0¦|1¦ with hid(e|ing) old one):
	return type: object
	get:
		set {_key} to expr-1
		replace all " " with "-" in {_key}
		if mark = 1:
			set {_bossbar} to Bukkit.getBossBar(NamespacedKey.fromString({_key}, Skript.getInstance()))
			if {_bossbar} is set:
				{_bossbar}.setVisible(false)
				Bukkit.removeBossBar(NamespacedKey.fromString({_key}, Skript.getInstance()))
		set {_bossbar} to Bukkit.createBossBar(new NamespacedKey(Skript.getInstance(), {_key}), expr-2, BarColor.valueOf(expr-4 in uppercase), BarStyle.valueOf(expr-5 in uppercase), null)
		{_bossbar}.setProgress(expr-3)
		if exprs-6 is set:
			loop exprs-6:
				{_bossbar}.addFlag(BarFlag.valueOf("%loop-expression%" in uppercase))
		loop exprs-7:
			{_bossbar}.addPlayer(loop-expression)
		return {_bossbar}

#
#	GETTING ALREADY CREATED BOSSBAR
#
#	This expression will find already created bossbar by its key (ID). Usefull when creating bossbar in any event and then need to use it in another.
#
#	EXAMPLE:
#	- set {_bossbar} to bossbar with key "welcome-bossbar"
#	- remove bossbar with key "welcome-bossbar"
#
expression boss[ ]bar (with|by) key %string%:
	return type: object
	get:
		set {_key} to expr-1
		replace all " " with "-" in {_key}
		set {_bossbar} to Bukkit.getBossBar(NamespacedKey.fromString({_key}, Skript.getInstance()))
		if {_bossbar} is set:
			return {_bossbar}
		else:
			return {_null}
			
#
#	GETTING/CHANGING/REMOVING BOSSBAR TITLE
#
#	This expression can change bossbar title, get it as string or clear it completely.
#	Only requirement is bossbar as object.
#
#	EXAMPLE:
#	- set {_bossbar} to bossbar with key "welcome-bossbar"
#	- set {_title} to title of bossbar {_bossbar}
#	- set title of bossbar {_bossbar} to "I hope you like this server!"
#	- remove title of bossbar {_bossbar}
#
expression title of boss[ ]bar %object%:
	return type: string
	get:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			return expr-1.getTitle().toString()
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
	set:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			expr-1.setTitle(change value)
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
	remove:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			expr-1.setTitle("&r")
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
#
#	GETTING/CHANGING BOSSBAR PROGRESS/DAMAGE
#
#	This expression can change bossbar progress, or get it as number (in JAVA double) value
#	Only requirement is bossbar as object.
#
#	EXAMPLE:
#	- set {_bossbar} to bossbar with key "welcome-bossbar"
#	- set {_value} to progress of bossbar {_bossbar} 
#	- set progress of bossbar {_bossbar} to 0.5
#
expression (progress|damage) of boss[ ]bar %object%:
	get:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			return expr-1.getProgress()
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
	set:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			expr-1.setProgress(change value)
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
#
#	GETTING/CHANGING BOSSBAR COLOR
#
#	This expression can change bossbar color, or get it's string value
#	Only requirement is bossbar as object.
#	Allowed colors are "BLUE","GREEN","PINK","PURPLE","RED","WHITE","YELLOW"
#
#	EXAMPLE:
#	- set {_bossbar} to bossbar with key "welcome-bossbar"
#	- set {_color} to color of bossbar {_bossbar} 
#	- set color of bossbar {_bossbar} to "YELLOW"
#
expression color of boss[ ]bar %object%:
	return type: string
	parse:
		set {_colors::*} to all boss bar colors
		continue
	get:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			return expr-1.getColor().toString()
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
	set:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			if {_colors::*} contains change value in uppercase:
				expr-1.setColor(BarColor.valueOf(change value in uppercase))
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
#
#	GETTING/CHANGING BOSSBAR STYLE
#
#	This expression can change bossbar style, or get it's string value
#	Only requirement is bossbar as object.
#	Allowed styles are "SOLID","SEGMENTED_6","SEGMENTED_10","SEGMENTED_12","SEGMENTED_20"
#
#	EXAMPLE:
#	- set {_bossbar} to bossbar with key "welcome-bossbar"
#	- set {_style} to style of bossbar {_bossbar} 
#	- set style of bossbar {_bossbar} to "YELLOW"
#
expression style of boss[ ]bar %object%:
	return type: string
	parse:
		set {_styles::*} to all boss bar styles
		continue
	get:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			return expr-1.getStyle().toString()
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
	set:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			if {_styles::*} contains change value in uppercase:
				expr-1.setStyle(BarStyle.valueOf(change value in uppercase))
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
#
#	GETTING/ADDING/REMOVING BOSSBAR FLAGS
#
#	This expression can add or remove certain bossbar flags, or get them as list.
#	Only requirement is bossbar as object.
#	Allowed flags are "CREATE_FOG","DARKEN_SKY","PLAY_BOSS_MUSIC"
#
#	EXAMPLE:
#	- set {_bossbar} to bossbar with key "welcome-bossbar"
#	- set {_flags::*} to flags of bossbar {_bossbar}
#	- add "PLAY_BOSS_MUSIC" to flags of bossbar {_bossbar} 
#	- remove "CREATE_FOG" from flags of bossbar {_bossbar}
#
expression flags of boss[ ]bar %object%:
	return type: strings
	parse:
		set {_flags::*} to all boss bar flags
		continue
	get:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			loop {_flags::*}:
				if expr-1.hasFlag(BarFlag.valueOf({_flags::%loop-index%})) = true:
					add {_flags::%loop-index%} to {_returnment::*}
			return {_returnment::*}
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
	add:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			if {_flags::*} contains change value in uppercase:
				broadcast "KS"
				expr-1.addFlag(BarFlag.valueOf(change value in uppercase))
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
	remove:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			if {_flags::*} contains change value in uppercase:
				expr-1.removeFlag(BarFlag.valueOf(change value in uppercase))
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")

#
#	GETTING/ADDING/REMOVING & RESETING PLAYERS SEEING BOSSBAR
#
#	This expression can add or remove certain players, or all of them, or it can get list
#	of players, who sees bossbar
#	Only requirement is bossbar as object.
#
#	EXAMPLE:
#	- set {_bossbar} to bossbar with key "welcome-bossbar"
#	- add event-player to all players seeing bossbar {_bossbar} 
#	- remove targeted player from all players seeing bossbar {_bossbar}
#
expression all players seeing boss[ ]bar %object%:
	return type: players
	get:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			return ...expr-1.getPlayers()
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
	add:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			if ...expr-1.getPlayers() does not contain change value:
				expr-1.addFlag(change value)
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
	remove:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			if change value = all players:
				expr-1.removeAll()
			if ...expr-1.getPlayers() contains change value:
				expr-1.removePlayer(change value)
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
	reset:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			expr-1.removeAll()
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
#
#	GETTING/CHANGING BOSSBAR VISIBILITY
#
#	This expression can change bossbar visibility to all players, no matter if you add
#	another player to see this bossbar. It can also get global visibility of bossbar
#	returned in boolean
#	Only requirement is bossbar as object.
#
#	EXAMPLE:
#	- set {_bossbar} to bossbar with key "welcome-bossbar"
#	- set {_cansee} to visibility of bossbar {_bossbar} 
#	- set visibility of bossbar {_bossbar} to false
#
expression visibility of boss[ ]bar %object%:
	return type: boolean
	get:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			return expr-1.isVisible()
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
	set:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			if change value = true:
				expr-1.setVisible(true)
			else if change value = false:
				expr-1.setVisible(false)
			else:
				Skript.warning("Value of setting visibility %change value% is not boolean!")
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")

#   ______                             __  __    __      __                               
#  /      \                           |  \|  \  |  \    |  \                              
# |  $$$$$$\  ______   _______    ____| $$ \$$ _| $$_    \$$  ______   _______    _______ 
# | $$   \$$ /      \ |       \  /      $$|  \|   $$ \  |  \ /      \ |       \  /       \
# | $$      |  $$$$$$\| $$$$$$$\|  $$$$$$$| $$ \$$$$$$  | $$|  $$$$$$\| $$$$$$$\|  $$$$$$$
# | $$   __ | $$  | $$| $$  | $$| $$  | $$| $$  | $$ __ | $$| $$  | $$| $$  | $$ \$$    \ 
# | $$__/  \| $$__/ $$| $$  | $$| $$__| $$| $$  | $$|  \| $$| $$__/ $$| $$  | $$ _\$$$$$$\
#  \$$    $$ \$$    $$| $$  | $$ \$$    $$| $$   \$$  $$| $$ \$$    $$| $$  | $$|       $$
#   \$$$$$$   \$$$$$$  \$$   \$$  \$$$$$$$ \$$    \$$$$  \$$  \$$$$$$  \$$   \$$ \$$$$$$$ 
#
#	BOSSBAR VISIBILITY CONDITION
#
#	This condition can get value of visibility of bossbar.
#	I know i made expression that can get this alos, but in bossbar interface, there is
#	this method to so i put it here to have it complete! :)
#	Only requirement is bossbar as object.
#
#	EXAMPLE:
#	- set {_bossbar} to bossbar with key "welcome-bossbar"
#	- if boss bar {_bossbar} is visible:
#	 	  set visibility of bossbar {_bossbar} to false
#
condition boss[ ]bar %object% (0¦is|1¦(is not|isn't)) visible:
	check:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			if mark = 0:
				if expr-1.isVisible() = true:
					continue
			else if mark = 1:
				if expr-1.isVisible() = false:
					continue
		else:
			Skript.warning("Variable %raw expr-1% is not instance of BossBar object!")
condition %object% (0¦is|1¦(is not|isn't)) boss[ ]bar:
	check:
		if expr-1 is an instance of (class "org.bukkit.boss.BossBar"):
			continue if mark = 0
		else if mark = 1:
			continue 

#
#  ________   ______    ______                        __               
# |        \ /      \  /      \                      |  \              
# | $$$$$$$$|  $$$$$$\|  $$$$$$\ ______    _______  _| $$_     _______ 
# | $$__    | $$_  \$$| $$_  \$$/      \  /       \|   $$ \   /       \
# | $$  \   | $$ \    | $$ \   |  $$$$$$\|  $$$$$$$ \$$$$$$  |  $$$$$$$
# | $$$$$   | $$$$    | $$$$   | $$    $$| $$        | $$ __  \$$    \ 
# | $$_____ | $$      | $$     | $$$$$$$$| $$_____   | $$|  \ _\$$$$$$\
# | $$     \| $$      | $$      \$$     \ \$$     \   \$$  $$|       $$
#  \$$$$$$$$ \$$       \$$       \$$$$$$$  \$$$$$$$    \$$$$  \$$$$$$$ 
#                                                      
#
#	DELETING ALREADY CREATED BOSSBAR
#
#	This effect can delete bossbar by knowing his key (ID).
#	YOU CANNOT DELETE BOSSBAR WITH HIS OBJECT.
#
#	EXAMPLE:
#	- remove boss bar with key "welcome-bossbar"
#
effect (remove|delete) boss[ ]bar (with|by) key %string%:
	trigger:
		set {_key} to expr-1
		replace all " " with "-" in {_key}
		set {_bossbar} to Bukkit.getBossBar(NamespacedKey.fromString({_key}, Skript.getInstance()))
		if {_bossbar} is set:
			{_bossbar}.setVisible(false)
			set {_bossbar} to Bukkit.removeBossBar(NamespacedKey.fromString({_key}, Skript.getInstance()))
#
#  ________                                             __                     
# |        \                                           |  \                    
# | $$$$$$$$ __    __  ______   ______ ____    ______  | $$  ______    _______ 
# | $$__    |  \  /  \|      \ |      \    \  /      \ | $$ /      \  /       \
# | $$  \    \$$\/  $$ \$$$$$$\| $$$$$$\$$$$\|  $$$$$$\| $$|  $$$$$$\|  $$$$$$$
# | $$$$$     >$$  $$ /      $$| $$ | $$ | $$| $$  | $$| $$| $$    $$ \$$    \ 
# | $$_____  /  $$$$\|  $$$$$$$| $$ | $$ | $$| $$__/ $$| $$| $$$$$$$$ _\$$$$$$\
# | $$     \|  $$ \$$\\$$    $$| $$ | $$ | $$| $$    $$| $$ \$$     \|       $$
#  \$$$$$$$$ \$$   \$$ \$$$$$$$ \$$  \$$  \$$| $$$$$$$  \$$  \$$$$$$$ \$$$$$$$ 
#                                            | $$                              
#                                            | $$                              
#                                             \$$                              
#
# USEFUL EXAMPLES:
#
#	1. MAKE BOSSBAR TO SHOW SOME MESSAGE FOR CERTAIN SECONDS, PROGRESS BAR IS SHOWING DURATION
#
#		every 30 seconds:
# 			set {_bossbar} to new boss bar with key "welcome-bossbar", title "<##34a4eb>&lWelcome to our server!", progress 0, color "BLUE", style "SOLID" for all players with hiding old one
# 			set {_sec} to 5 # Amount of seconds to appear
# 			set {_d} to 1/({_sec}*20)
# 			loop ({_sec}*20) times:
# 				set progress of bossbar {_bossbar} to (loop-value)*{_d}
# 				wait 1 tick
# 			remove bossbar with key "welcome-bossbar"
# 	
#	2. MAKE BOSSBAR TO WATCH HP OF SOME ENTITY (IN THIS EXAMPLE I AM USING SHEEP)
#
#		on spawn of sheep:
# 			set {_uuid} to random uuid # Ability to have multiple sheep
# 			set {_names::*} to "Merlin","Tobby","Krazy","Milo","Bobby" # Random names :3
# 			set {_bossbar} to new boss bar with key "sheep_%{_uuid}%", title "&lMAGIC SHEEP - %random element of {_names::*} in uppercase%", progress 1, color "WHITE", style "SOLID" for all players with hiding old one
# 			set {_max} to max health of event-entity
# 			set {_h} to health of event-entity
# 			while event-entity is alive:
# 				set progress of bossbar {_bossbar} to (1/{_max})*{_h}
# 				set {_h} to health of event-entity
# 				wait 1 tick
# 			set progress of bossbar {_bossbar} to 0 #If i instakill it, it will stay to 1 so i will set it to 0 there
# 			wait 1 second #It takes 1 second to mob to disapear after death
# 			remove bossbar by key "sheep_%{_uuid}%"
#
#	3. MAKE COUNTDOWN COMMAND AND SHOW TIMER THROUGH BOSSBAR
#
# 		on load:
# 			set {-bossbar} to new boss bar with key "countdown", title "<##ffff00>&lEVENT IS ENDING IN: xs", progress 1, color "YELLOW", style "SOLID" for all players with hiding old one
# 			set visibility of boss bar {-bossbar} to false
#
# 		command /countdown <integer>:
# 			trigger:
# 				set {_d} to 1/(arg-1)
# 				set visibility of boss bar {-bossbar} to true
# 				loop arg-1 times:
# 					set {_time} to (arg-1)-((loop-value)-1)
# 					if {_time} <= 3:
# 						set color of boss bar {-bossbar} to "RED"
# 						set title of boss bar {-bossbar} to "<##ff0000>&lEVENT IS ENDING IN: %{_time}%s"
# 						set progress of boss bar {-bossbar} to 1-({_d}*((loop-value)-1))
# 						wait 0.5 second
# 						stop if "%progress of boss bar {-bossbar}%" is not "%1-({_d}*((loop-value)-1))%"
# 						set color of boss bar {-bossbar} to "YELLOW"
# 						set title of boss bar {-bossbar} to "<##ffff00>&lEVENT IS ENDING IN: %{_time}%s"
# 						wait 0.5 second
# 						stop if "%progress of boss bar {-bossbar}%" is not "%1-({_d}*((loop-value)-1))%"

# 					else:
# 						set title of boss bar {-bossbar} to "<##ffff00>&lEVENT IS ENDING IN: %{_time}%s"
# 						set progress of boss bar {-bossbar} to 1-({_d}*((loop-value)-1))
# 						wait 1 second
# 						stop if "%progress of boss bar {-bossbar}%" is not "%1-({_d}*((loop-value)-1))%"
# 				set title of boss bar {-bossbar} to "<##ffff00>&lEVENT ENDED!"
# 				set progress of boss bar {-bossbar} to 0
# 				wait 1 second
# 				if progress of boss bar {-bossbar} is 0:
# 					set visibility of boss bar {-bossbar} to false 
#
#	4. MAKE BOSSBAR TO FADE MESSAGE CHARACTER BY CHARACTER WITH PROGRESS BAR FADING AT SAME SPEED AS SHOWING MESSAGE FOR CERTAIN SECONDS,
#	   AND FADE IT BACK AWAY
#
# 		command /bossbar:
# 			trigger:
# 				set {_bossbar} to new boss bar with key "show", title "&l", progress 0, color "WHITE", style "SOLID" for all players with hiding old one
# 				set {_text} to "WELCOME HERE! ENJOY YOUR STAY!"
# 				set {_chars::*} to split {_text} at ""
# 				loop (length of {_text}) times:
# 					wait 1 tick
# 					set title of boss bar {_bossbar} to join (title of boss bar {_bossbar}),{_chars::%loop-value%}
# 					set progress of boss bar {_bossbar} to 1/(length of {_text})*(loop-value)
# 				wait 5 second
# 				loop (length of {_text}) times:
# 					wait 1 tick
# 					set {_title} to title of boss bar {_bossbar}
# 					set {_tchars::*} to split {_title} at ""
# 					set {_return} to ""
# 					loop (length of {_title})-1 times:
# 						set {_return} to join {_return},{_tchars::%loop-value-2%}
# 					set title of boss bar {_bossbar} to join "&l",{_return}
# 					set progress of boss bar {_bossbar} to 1-(1/(length of {_text})*(loop-value))
# 				remove bossbar by key "show"